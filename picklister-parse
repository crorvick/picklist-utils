#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

my @todo;
my $output;

my %outfiles;
my $op_count = 0;

sub process_op_string {
	my ($op, $ifmt, $ofmt, $args) = (@_, "", "");

	while ($ifmt =~ /^([^%]*%)(.*)/) {
		$ofmt .= $1;
		my $rem = $2;
		if ($rem =~ /^%(.*)/) {
			$ofmt .= "%";
			$ifmt = $1;
		} elsif ($rem =~ /^(-?\d*\.?\d*)([nWPCI])(.*)/) {
			$ofmt .= $1;
			$ofmt .= $2 eq 'n' ? 'd' : 's';
			$args .= $2;
			$ifmt = $3;
		} else {
			die;
		}
	}

	$ofmt .= $ifmt;

	my $n = $op_count++;

	return "$n:$op:$args:$ofmt";
}

sub process_output_template {
	my ($template, $wo, $filename) = (@_, "");

	return if not $template;

	while ($template =~ /^([^%]*)%(.*)/) {
		$filename .= $1;
		my $rem = $2;
		if ($rem =~ /^%(.*)/) {
			$filename .= "%";
			$template = $1;
		} elsif ($rem =~ /^(-?\d*\.?\d*)W(.*)/) {
			return if not $wo;
			my $fmt = "%$1s";
			$filename .= sprintf("$fmt", $wo);
			$template = $2;
		} else {
			die;
		}
	}

	return ($filename . $template);
}

Getopt::Long::Configure ("bundling");
GetOptions(
	'p|print=s' => sub { push @todo, process_op_string(@_) },
	'x|exec=s'  => sub { push @todo, process_op_string(@_) },
	'o|output=s'=> \$output,
	'h|help'    => sub {pod2usage(1) },
	'man'       => sub {pod2usage(-exitstatus => 0, -verbose => 2) }
) or pod2usage(2);

push @todo, process_op_string("p", "%W %I") if not @todo;

my %items;

sub process_item {
	my $n   = shift @_;
	my $wo  = shift @_;
	my $pi  = shift @_;
	my $ci  = shift @_;

	my $count = 0;

	todo: for (@todo) {
		my ($opn, $op, $args, $fmt) = split(/:/, $_, 4);
		my %key = ( 0 => $opn );
		my @args;

		for my $arg (split(//, $args)) {
			if ($arg eq 'n') {
				push @args, $n;
			} elsif ($arg eq 'W') {
				$key{1} = $wo;
				push @args, $wo;
			} elsif ($arg eq 'P') {
				$key{1} = $wo;
				$key{2} = $pi;
				push @args, $pi;
			} elsif ($arg eq 'C') {
				next todo if not $ci;
				$key{1} = $wo;
				$key{2} = $pi;
				$key{3} = $ci;
				push @args, $ci;
			} elsif ($arg eq 'I') {
				$key{1} = $wo;
				$key{2} = $pi;
				$key{3} = $ci if $ci;
				push @args, $ci // $pi;
			} else {
				push @args, '<???>';
			}
		}

		my $key = join('|', map($key{$_}, sort(keys %key)));
		next if (exists($items{$key}));

		$items{$key} = $n;

		my $s = sprintf("$fmt &\n", @args);

		if ($op eq "p") {
			print $s;
		} elsif ($op eq "x") {
			system($s);
		}

		$count += 1;
	}

	return $count;
}

sub process_page {
	my $wo = shift @_;
	my $filename = process_output_template($output, $wo);

	return if not $filename;

	my $fh;
	if (not exists $outfiles{$filename}) {
		if (not $filename eq '-') {
			open($fh, ">", $filename);
		} else {
			$fh = *STDOUT;
		}
		$outfiles{$filename} = $fh;
	} else {
		$fh = $outfiles{$filename};
		print $fh "\x0c";
	}

	for my $line (@_) {
		print $fh "$line\n";
	}
}

sub process_report {
	my ($wo, $pi);
	my $n = 0;
	my $state = 'MAIN';

	my @lines;

	while (<>) {
		chomp;

		my $eof = /End of Report/;
		my $brk = ();

		if (/^\x0c(.*)/) {
			$_ = $1;
			$brk = 1;
			$state = 'MAIN';
		}

		if ($brk || $eof) {
			process_page($wo, @lines);
			undef $wo;
			undef $pi;
			undef @lines;
		}

		last if ($eof);

		push @lines, $_;

		if ($state eq 'MAIN') {
			if (/Work Order:\s*(\S*)/) {
				$wo = $1;
			} elsif (/Item Number:\s*(\S*)/) {
				$pi = $1;
				$n += 1 if process_item($n+1, $wo, $pi);
			} elsif (/^Item Number\s/) {
				$state = 'ITEM_LIST';
			}
		} elsif ($state eq 'ITEM_LIST') {
			next if (/^-/);     # ignore horizontal rule

			if (/^(\S+)/) {
				$n += 1 if process_item($n+1, $wo, $pi, $1);
			}
		}
	}
}

process_report;

__END__

=head1 NAME

picklister-parse - read CAT pick-list report

=head1 SYNOPSIS

B<picklister-parse> [OPTION]... [REPORT]...

 Options:
    --print=FORMAT
    --exec=FORMAT
    --output=FILE

=head1 DESCRIPTION

Process pick-list per the specified operations.

    --print=FORMAT
        Print FORMAT for each occurance of unique output.  Supports the
        following conversion specifiers:

          %W - work order
          %P - parent item number
          %C - component item number
          %I - item number (parent or component)
          %n - count incremented each time FORMAT is printed

    --exec=FORMAT
        Execute FORMAT for each occurance of unique output.  Supports the
        same conversion specifiers as --print.

    --output=FILE
        Write pick-list report text to FILE.  Reports can be split into
        multiple files using the following conversion specifiers:

          %W - work order

=head1 AUTHOR

Chris Rorvick <chris@rorvick.com>
